;--- Объявление переменных ---
T0_ini		EQU	1000		; начальное значение таймера
arr_len		EQU	6		; количество выводимых цифр
RG2		EQU	0A000h		; адрес регистра RG2
RG3		EQU	0B000h		; адрес регистра RG3
D0_ini		EQU 	01111111b	; ноль определяет разряд на дисплее, будем сдвигать его вправо

;--- Начало сегмента данных ---
	dseg at 30h
ID_cur:		ds	1		; номер текущего элемент массива 
D0_cur:		ds	1		; текущее значение в [RG2]

;--- Начало сегмента кода ---
	cseg 
	jmp	start

	org	000Bh			; прерывание от таймера
	call	timer
	reti

;--- Старт программы ---			  
start:
	mov 	ID_cur, #0		; инициализация
	mov 	D0_cur, #D0_ini

	mov 	TMOD, #00000001b	; таймер в режиме 1
	mov 	IE, #10000010b		; разрешение прерываний от таймера
	
;--- Установка частоты прерываний ---
	mov 	TH0, #high(65535-T0_ini)	; сброс старшего разряда
	mov 	TL0, #low(65535-T0_ini)		; сброс младшего разряда
	setb 	TR0			; запуск таймера
	
	jmp $

;--- Обработчик прерываний от таймера ---
timer:
	mov 	TH0, #high(65535-T0_ini) 
	mov 	TL0, #low(65535-T0_ini)
	
	mov 	dptr, #RG2		; Записываем в указатель памяти адрес разрядов
	mov 	a, #10000001b			
	movx 	@dptr, a		; Гасим все сегменты

;--- Делаем текущим следующий сегмент ---
	mov 	a, D0_cur
	rr 	a
	mov 	D0_cur, a

;--- Вывод информации на дисплей ---
	mov 	dptr, #RG2			
	mov 	a, D0_cur			
	movx 	@dptr, a			
	
	;--- ВЫБОР ЭЛЕМЕНТА ИЗ МАССИВА ---
	mov 	a, ID_cur
	mov 	dptr, #digits_array
	movc 	a, @a+dptr

	mov 	dptr, #RG3
	movx 	@dptr, a
	
	mov 	a, ID_cur
	inc 	a
	cjne 	a, #arr_len, end_timer ; Проверка на конец кадра

;--- Кадр закончился ---
	mov 	ID_cur, #0		; Делаем нулевой разряд текущим 
	mov 	D0_cur, #D0_ini

	ret

;--- Кадр не закончился ---
end_timer:
	mov 	ID_cur, a	 	; Обновляем текущую позицию
	ret

org 0FFh
;╔═════════╦═════════╦═════════╦═════════╦═════════╗
;║    0    ║    1    ║    2    ║    3    ║    4    ║
;║00111111b║00000110b║01011011b║01001111b║01100110b║
;╚═════════╩═════════╩═════════╩═════════╩═════════╝	
;╔═════════╦═════════╦═════════╦═════════╦═════════╗
;║    5    ║    6    ║    7    ║    8    ║    9    ║
;║01101101b║01111101b║00000111b║01111111b║01101111b║
;╚═════════╩═════════╩═════════╩═════════╩═════════╝
					
digits_array: db 00000110b, 01011011b, 01101111b, 01100110b, 01111101b, 00111111b

end